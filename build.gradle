plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.4'
    id 'io.spring.dependency-management' version '1.1.4'
    id "org.openapi.generator" version "7.4.0"
}

group = 'xyz.optimized'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '21'
}

ext {
    mapstructVersion = "1.6.0.Beta1"
    lombokMapstructBindingVersion = "0.2.0"
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation('org.springframework.hateoas:spring-hateoas')
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.21'
    implementation 'jakarta.validation:jakarta.validation-api:3.0.2'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'

    // https://mvnrepository.com/artifact/org.mapstruct/mapstruct
    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
//    implementation 'org.mapstruct:mapstruct-processor:${mapstructVersion}'

    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'io.r2dbc:r2dbc-h2'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    //Mapstruct
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    annotationProcessor "org.projectlombok:lombok-mapstruct-binding:${lombokMapstructBindingVersion}"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

openApiGenerate {
    generatorName.set("spring")
    inputSpec.set("$rootDir/src/main/resources/apis/jobs.yaml")
    outputDir.set("$rootDir/src/main/gen")
    apiPackage.set("xyz.optimized.jobs.apifirst.api")
//    invokerPackage.set("com.example.apifirstgradle1.invoker")
    modelPackage.set("xyz.optimized.jobs.apifirst.model")
    configOptions.set([
            dateLibrary   : "java17",
            interfaceOnly : "true",
            useSpringBoot3: "true",
            reactive      : "true",
            hateoas       : "true"

    ])
}

sourceSets {
    main {
        java {
            srcDirs = ["src/main/java", "src/main/gen/src/main/java"]
        }
    }
}

tasks.register("deleteGeneratedSources") {
    doFirst {
        delete "$rootDir/src/main/gen"
    }
}
